pkgname=p0f
pkgver=3.06b
pkgrel=3
pkgdesc="Purely passive TCP/IP traffic fingerprinting tool "
url="http://lcamtuf.coredump.cx/p0f3/"
arch=('x86_64' 'i686')
license=('GPL3')
groups=(blackarch blackarch-forensics blackarch-intel)
depends=('libpcap')
install=p0f.install
source=("http://lcamtuf.coredump.cx/p0f3/releases/$pkgname-$pkgver.tgz")
md5sums=('1a07d6220d9e57d8d098905b7ca6844a')

# Additional tools: p0f-client & p0f-sendsyn
_p0ftools=true
# Additional tools: p0f-sendsyn6 (ipv6 p0f-sendsyn)
_p0ftools6=true

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -e 's/\/usr\/local\/include\//\/usr\/include\//g' -i build.sh
  sed -e 's/\/usr\/local\/lib\//\/usr\/lib\//g' -i build.sh
  sed -e 's/"p0f.fp"/"\/etc\/p0f\/p0f.fp"/g' -i config.h
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make

  # Additional tools.
  if $_p0ftools ; then
    cd "${srcdir}/${pkgname}-${pkgver}/tools"
    make p0f-client
    make p0f-sendsyn
  fi
  if $_p0ftools6 ; then
    cd "${srcdir}/${pkgname}-${pkgver}/tools"
    make p0f-sendsyn6
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -dm755 ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/etc/p0f
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}

  install -m755 p0f ${pkgdir}/usr/bin
  install -m755 p0f.fp ${pkgdir}/etc/p0f
  install -Dm644 docs/COPYING ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 docs/README ${pkgdir}/usr/share/doc/${pkgname}

  # Additional tools.
  if  $_p0ftools ; then
    install -m755 tools/p0f-client ${pkgdir}/usr/bin
    install -m755 tools/p0f-sendsyn ${pkgdir}/usr/bin
  fi
  if $_p0ftools6 ; then
    install -m755 tools/p0f-sendsyn6 ${pkgdir}/usr/bin
  fi
}
