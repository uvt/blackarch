pkgname=ace
groups=(blackarch blackarch-intel)
pkgver=1.10
pkgrel=2
pkgdesc="Automated Corporate Enumerator. A simple yet powerful VoIP Corporate Directory enumeration tool that mimics the behavior of an IP Phone in order to download the name and extension entries that a given phone can display on its screen interface"
url="http://ucsniff.sourceforge.net/ace.html"
license=('GPL')
depends=('libpcap')
arch=(i686 x86_64)
source=(http://downloads.sourceforge.net/ucsniff/$pkgname-$pkgver.tar.gz)
md5sums=('42788723aeb1c41145371e49a64d5bc5')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's#linux/if_tr.h#netinet/if_tr.h#' client.h
  sed -i "s:gcc :gcc $CFLAGS :g" Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
}
