pkgname='rainbowcrack'
groups=(blackarch blackarch-password-attacks)
pkgver=1.2
pkgrel=4
pkgdesc='Password cracker based on the faster time-memory trade-off. With MySQL and Cisco PIX Algorithm patches'
arch=('i686' 'x86_64')
url="http://project-rainbowcrack.com/"
license=('custom')
depends=('openssl')
source=("$pkgname-$pkgver-src.tar.xz"
        "http://www.oxid.it/downloads/rc_patch_mysql_pix.zip")
md5sums=('60137c06b727d6d3ca50340ff2c68292'
         '71845ac352ff888a9cfcff6c1ea4184b')

build() {
  cd "$srcdir/$pkgname-$pkgver-src/src"

  # MySQL and Cisco PIX Algorithm patches
  install -Dm644 ${srcdir}/*.{h,cpp} .

  # Missing headers
  sed -i "11i#include <stdlib.h>\n#include <string.h>" Public.h

  # DSO link fix for crypto
  sed -i "s/-lssl/& -lcrypto/" makefile.linux

  make -f makefile.linux
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src/src"

  # Bin.
  install -d "$pkgdir/usr/bin"
  install -Dm755 r{t,c}* "$pkgdir/usr/bin"

  # Data.
  install -Dm644 "$srcdir/readme.txt" \
    "$pkgdir/usr/share/$pkgname/patches.txt"
  install -Dm644 charset.txt ../readme.txt \
    "$pkgdir/usr/share/$pkgname"

  # Docs.
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 ../doc/* \
    "$pkgdir/usr/share/doc/$pkgname"

  # License.
  install -Dm644 ../disclaimer.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
